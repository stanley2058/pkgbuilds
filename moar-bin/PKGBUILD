# Maintainer: Stanley Wang <stanley@stw.tw>

pkgname=moar
pkgver=1.31.5
pkgrel=1
pkgdesc="A pager designed to just do the right thing without any configuration."
arch=(any)
provides=(moar)
conflicts=(moar-git)
url='https://github.com/walles/moar'
license=(BSD)
makedepends=(go gzip)
optdepends=(highlight)
checkdepends=(highlight)
source=("https://github.com/walles/moar/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('2c03100e24f88163d808d52b7a5763c0fe731b29c46fd44a103a089c5429eb72')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    go build -ldflags="-X main.versionString=${pkgver}" -o "${pkgname}"
    gzip "moar.1"
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    go test github.com/walles/moar/m
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -dm755 "${pkgdir}/usr/bin"
    install -m755 "moar" "${pkgdir}/usr/bin/moar"
    install -dm755 "${pkgdir}/usr/share/man/man1"
    install -m0644 "moar.1.gz" "${pkgdir}/usr/share/man/man1/moar.1.gz"
}
